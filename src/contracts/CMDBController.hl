module CMDBController

import {
    mkTv,
    tvCharter
} from StellarHeliosHelpers

func requiresPageInput(
    ctx: ScriptContext,
    mph: MintingPolicyHash,
    pageEid: String
) -> (Value, TxInput) {
    pageUutVal : Value = mkTv(mph, pageEid);
    pageTxIn : TxInput = ctx.tx.inputs.find_safe((txin: TxInput) -> {
        txin.value >= pageUutVal
    }).switch{
        Some{txin} => txin,
        None => error("no reqd input w/ "+ pageEid + 
            "\n expected: "+pageUutVal.show()
        )
    };
    (pageUutVal, pageTxIn)
}

func requiresAcceptingChangesActivity(ctx: ScriptContext, pageUtxo : TxInput ) -> Bool {
    print("seeking acceptingChanges redeemer:" );
    print(" - expected "+ pageUtxo.output_id.serialize().show());
    ctx.tx.redeemers.any( 
        (purpose : ScriptPurpose, redeemer: Data) -> { purpose.switch{ 
            sp: Spending => {
                print (" - redeemer " + sp.output_id.serialize().show());
                sp.output_id == pageUtxo.output_id &&
                redeemer.switch {
                    (index: Int, fields: []Data) => {
                        isExpected: Bool = (index == 4);
                        assert(isExpected, "wrong pg-level activity for mintDgt burnSuggestionsBeingAccepted: want index 4, got "+ index.show());
                        // print ("right redeemer with index "+ index.show()+" hasExpectedIndex=" + isExpected.show());
                        isExpected && // acceptingChanges to a page
                        fields == fields
                    },
                    _ => error("incontheevable")
                }
            },
            _ => false 
        } }
    )    
}

struct BookEntryStruct {
    entryType: String
    title: String
    content: String
    pmSteps: Option[String]
    createdAt: Int
    updatedAt: Int
    updatedBy: String
    expiresAt: Int
    //  suggestedBy: String // XXX collaborator-id  - see delegation-link, whose content serves the same purpose.
    appliedChanges: []String
    //! TODO: rejectedChanges: []String 
    changeParentEid: Option[String]
    changeParentTxId: Option[TxId] // -> txid
    changeParentOidx: Option[Int]

    func validate(self, 
        ctx: ScriptContext, 
        mph: MintingPolicyHash
    ) -> Bool {
        goodTypes = Map[String]Bool{
            "pg":true, //page
            "spg": true,  // suggested page
            "sug": true  // suggested change
        };

        needsLength : (String, String, Int) -> () = (v : String, fn : String, min : Int) -> {
            if(v.encode_utf8().length < min) {
                error(fn + ": fld len <" + min.show())
            }
        };

        if(self.updatedAt > 0) {
            assert(self.updatedBy.starts_with("collab-"), "no reqd updatedBy")
        } else {
            assert(self.updatedBy.encode_utf8().length == 0, "non-empty updatedBy")
        };

        assert(goodTypes.get_safe(self.entryType).switch{
            None => {
                error("invalid entryType: '"+ self.entryType + "'")
            },
            _ => true
        }, ""); // already failed as invalid ^^

        validatesOk : Bool = if ("sug" == self.entryType) {
            assert(
                self.title.encode_utf8().length >0 || 
                self.content.encode_utf8().length > 0,
                 "no chg in title or content"
            );
            assert(
                self.pmSteps.switch{
                    None => false,
                    Some{pm} => pm.encode_utf8().length > 0
                }, "no reqd pmSteps"
            );
            peLength: Int = self.changeParentEid.switch{
                None => error("no reqd changeParentEid"),
                Some{peid} => peid.encode_utf8().length
            };
            assert(
                peLength > 15, 
                "chg peid too short: "+peLength.show()
            );

            // the value of the id-token:
            parentRec : Value = mkTv(mph, self.changeParentEid.unwrap());

            self.changeParentTxId.switch {
                Some{txid} => {
                    ctx.tx.ref_inputs.find_safe((txin: TxInput) -> {
                        txin.output_id.tx_id == txid &&
                        txin.output.value >= parentRec
                    }).switch{
                        Some => {
                            self.changeParentOidx.switch{
                                Some => true,
                                None => error("no reqd changeParentOidx")
                            } &&
                            true
                        },
                        None => error("no ref_input for chg parent")
                    }
                },
                _ => error("no reqd changeParentTxId")
            }        
        } else {
            needsLength(self.title, "title", 10);
            needsLength(self.content, "content", 40);
            self.pmSteps.switch {
                Some => error("pmSteps not ok"),
                None => true
            } &&
            true    
        };
        
        validatesOk
    }
}
